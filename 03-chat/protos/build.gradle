plugins {
    id "java"
    // Activación del plugin de protocol buffers
    id "com.google.protobuf" version "0.9.4"
}

def grpcVersion = '1.59.1'
def protobufVersion = '3.24.0'

repositories {
    mavenCentral()
}

dependencies {
    // Dependencias de grpc
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"
}

// Configuración de la generación de código
protobuf {
    protoc {
        // Configuración de la versión de protoc a utilizar
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        // Configuración de la generación de código de grpc
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        // Se añaden a las fuentes de código las carpetas en las que se genera el código de grpc
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}